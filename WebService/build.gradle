import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'war'
    id 'org.gretty' version '3.0.2'
    id "com.heroku.sdk.heroku-gradle" version "1.0.4"
}


configurations.create('download')
dependencies {
    implementation project(':AuthCLI')

    implementation 'javax:javaee-api:7.0'
    implementation 'com.google.inject.extensions:guice-servlet:4.2.3'

    implementation "org.postgresql:postgresql:42.2.12"

    download 'org.eclipse.jetty:jetty-runner:9.4.9.v20180320'
}

gretty {
    contextPath = '/'
}


task stage() {
    dependsOn clean, war
}
war.mustRunAfter clean

task copyToLib(type: Copy) {
    into "$buildDir/server"
    from(configurations.download) {
        include "jetty-runner*"
    }
}

stage.dependsOn(copyToLib)


gradle.taskGraph.whenReady {
  taskGraph ->
    if (taskGraph.hasTask(stage)) {
      test.enabled = false
    }
}

task buildWebapp(type: Exec) {
    workingDir "../"
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine "cmd", "/c", "npm run build"
    }
    else {
        commandLine "sh", "-c", "npm run build"
    }
}

stage.dependsOn(buildWebapp)
